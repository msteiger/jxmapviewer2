apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'eclipse'
apply plugin: 'idea'

group = 'org.jxmapviewer'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'commons-logging', name: 'commons-logging', version: '1.2'
}


uploadArchives {
    repositories {
        if (project.hasProperty('sonatypeRepoURI') &&
            project.hasProperty('sonatypeUsername') &&
            project.hasProperty('sonatypePassword')) {

            mavenDeployer {
                beforeDeployment { MavenDeployment deployment ->
                    signing.signPom(deployment)
                }

                repository(url: sonatypeRepoURI) {
                    authentication(
                        userName: sonatypeUsername,
                        password: sonatypePassword);
                }

                pom.project {
                    name 'jxmapviewer2'
                    packaging 'jar'
                    description 'A MapViewer based on the JXMapViewer component of SwingX-WS.'
                    url 'https://github.com/msteiger/jxmapviewer2'

                    scm {
                        url 'git@github.com:msteiger/jxmapviewer2.git'
                        connection 'scm:git@github.com:msteiger/jxmapviewer2.git'
                        developerConnection 'scm:git@github.com:msteiger/jxmapviewer2.git'
                    }

                    issueManagement {
                        url 'https://github.com/msteiger/jxmapviewer2/issues'
                    }

                    licenses {
                        license {
                            name 'GNU Lesser General Public License'
                            url 'http://www.gnu.org/licenses/lgpl-3.0-standalone.html'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'msteiger'
                            name 'Martin Steiger'
                        }
                    }
                }
            }
        }
    }
}

processResources {
    filesMatching('project.properties') {
        expand projectVersion: project.version,
               projectName: project.name
    }
}

wrapper {
    gradleVersion = '6.0.1'
}

jar {
    manifest {
        attributes('Automatic-Module-Name': 'org.jxmapviewer.jxmapviewer2')
    }
}

task sourceJar(type: Jar) {
    description = "Create a JAR with all sources"
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    description = "Create a JAR with the JavaDoc for the java sources"
    from javadoc.destinationDir
    classifier = 'javadoc'
}

// Define the artifacts we want to publish (the .pom will also be included since the Maven plugin is active)
artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

signing {
    if (project.hasProperty('signing.keyId') &&
        project.hasProperty('signing.password') &&
        project.hasProperty('signing.secretKeyRingFile')) {
        sign configurations.archives
    }
}
